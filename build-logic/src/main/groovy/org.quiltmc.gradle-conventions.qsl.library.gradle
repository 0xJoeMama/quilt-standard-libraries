plugins {
	id "maven-publish"
	id "org.ajoberstar.grgit"
	id "org.quiltmc.gradle-conventions.qsl.common"

}

group = "org.quiltmc.qsl-prototype"
version = project.qsl_library_version
def getHash() {
	if (grgit == null) {
		return "nogit"
	} else {
		def latestCommits = grgit.log(paths: [project.path.substring(1).replace(':', '/')], maxCommits: 1)

		if (latestCommits.isEmpty()) {
			return "uncommitted"
		} else {
			return latestCommits.get(0)
		}
	}
}
afterEvaluate {
	task runTestmodClient(type: org.quiltmc.loom.task.RunClientTask) {
		subprojects.each {
			classpath += it.sourceSets.testmod.runtimeClasspath
		}
	}
	task runTestmodServer(type: org.quiltmc.loom.task.RunServerTask) {
		subprojects.each {
			classpath += it.sourceSets.testmod.runtimeClasspath
		}
	}
	task runAutoTestServer(type: org.quiltmc.loom.task.RunServerTask) {
		subprojects.each {
			classpath += it.sourceSets.testmod.runtimeClasspath
		}
		jvmArgs "-Dquilt.autoTest"
		args "--nogui"
	}
}

jar {
	enabled = false
}
remapJar {
	enabled = false
}

task checkLibVersion {
	doFirst {
		try {
			def xml = new URL("https://maven.quiltmc.org/repository/release/org/quiltmc/qsl/$project.qsl_library/$project.version/" +
					"$project.qsl_library-$project.qsl_library_version" + ".pom").text
			def metadata = new groovy.xml.XmlSlurper().parseText(xml)
			if (metadata.properties.hash != getHash()) {
				throw new RuntimeException("Library is already published with a different hash!")
			}
		} catch (FileNotFoundException ignored) {
			//
		}
	}
}
project.getTasksByName("checkModuleVersion", true).each {
	checkLibVersion.mustRunAfter it
}
publishing {
	repositories {
		mavenLocal()
	}

	publications {
		mavenJava(MavenPublication) {
			pom.withXml {
				asNode().appendNode("properties").appendNode("hash", getHash())
				def depsNode = asNode().appendNode("dependencies")
				rootProject.subprojects.stream().filter {
					it.path.startsWith(":$qsl_library:")
				}.forEach {
					def depNode = depsNode.appendNode("dependency")
					depNode.appendNode("groupId", it.group)
					depNode.appendNode("artifactId", it.name)
					depNode.appendNode("version", it.version)
					depNode.appendNode("scope", "compile")
				}
			}
		}
	}
}

publish.dependsOn checkLibVersion
