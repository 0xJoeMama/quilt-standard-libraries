import java.nio.file.Files

pluginManagement {
    repositories {
		gradlePluginPortal()
		mavenCentral()

		maven {
			name = "Quilt"
            url = "https://maven.quiltmc.org/repository/release"
        }

        maven {
            name = "ajoberstar-backup"
            url = "https://ajoberstar.org/bintray-backup/"
        }
    }
}

rootProject.name = "qsl"

includeBuild("build-logic")

def includeLib(String library) {
	include(library)
	project(":$library").projectDir = file("src/$library")

	rootProject.projectDir.toPath().resolve("src/$library/").toFile().listFiles().each {
		if (it.isDirectory() && it.name != "src" && it.name != "build" && !it.name.startsWith(".")) {
			def properties = new Properties()
			properties.load(Files.newBufferedReader(it.toPath().resolve("gradle.properties")))

			if (!properties.qsl_disabled) {
				include "$library:$it.name"
			}
		}
	}
}

includeLib("core")
