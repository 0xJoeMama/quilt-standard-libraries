import java.nio.file.Files

pluginManagement {
    repositories {
        maven {
            url = "https://maven.quiltmc.org/repository/release"
        }
        maven {
            name = 'ajoberstar-backup'
            url = 'https://ajoberstar.org/bintray-backup/'
        }
        mavenCentral()
        gradlePluginPortal()
    }

}
rootProject.name = 'qsl'
def includeLib(String string) {
	include string
	project(":$string").projectDir = file("src/$string")
	rootProject.projectDir.toPath().resolve("src/$string/").toFile().listFiles().each {
		if (it.isDirectory() && it.name != "src" && it.name != "build" && !it.name.startsWith(".")) {
			def properties = new Properties()
			properties.load(Files.newBufferedReader(it.toPath().resolve("gradle.properties")))
			if (!properties.qsl_disabled) {
				include "$string:$it.name"
			}
		}
	}
}

includeLib("core")
